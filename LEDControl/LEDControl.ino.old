#include <SPI.h>
#include <Ethernet.h>
#include "FastSPI_LED2.h"

#define NUM_LEDS  80
#define DATA_PIN  8
#define CLOCK_PIN 9

CRGB leds[NUM_LEDS];

enum Mode {
  SOLIDWHITE,
  MARQUEE,
  COLORCYCLE,
  PEW
};

EthernetClient client;
byte           mac[] = {0xab, 0xcd, 0xef, 0x12, 0x34, 0x56};
IPAddress      ip(10,27,29,100);
IPAddress      robotIP(10,27,29,2);
int            port = 41900; //TODO get the port

long WHITE   = 0xFFFFFF;
long BLACK   = 0x000000;
long RED     = 0xFF0000;
long GREEN   = 0x00FF00;
long BLUE    = 0x0000FF;
long YELLOW  = 0xFFFF00;
long MAGENTA = 0xFF00FF;
long CYAN    = 0x00FFFF;

class LEDMode {
  public:
    virtual void doLoop();
    virtual void reset();
};

class SolidWhite : public LEDMode {
  public:
    void doLoop() {
      for (int i = 0; i < NUM_LEDS; i++) {
        leds[i] = WHITE;
      }
    }
    void reset() {};
};

//TODO don't run always
class Marquee : public LEDMode {
  int count        = 0;
  int delayCounter = 0;

  public:
    void doLoop() {
      if (delayCounter == 0){
        for (int i = 0; i < NUM_LEDS; i++){
          if ((i + count) % 3 != 0) leds[i] = BLACK;
          else{
            switch (random(4)){
              case 0:  leds[i] = WHITE;
                       break;
              case 1:  leds[i] = RED;
                       break;
              case 2:  leds[i] = GREEN;
                       break;
              case 3:  leds[i] = BLUE;
                       break;
              default: break;
            }
          }
        }
        
        count++;
        count %= 3;
      }

      
      delayCounter++;
      delayCounter %= 3;
    }
    void reset(){
      count = 0;
      delayCounter = 0;
    }
};

class ColorCycle : public LEDMode {
  int count = 0;
  int delayCounter = 0; //To make it not change every time it cycles, which is 20 times per second

  public:
    void doLoop(){
      if (delayCounter == 0) {
        for (int i = 0; i < NUM_LEDS; i++){
          switch (count){
            case 0:  leds[i] = WHITE;
                     break;
            case 1:  leds[i] = RED;
                     break;
            case 2:  leds[i] = GREEN;
                     break;
            case 3:  leds[i] = BLUE;
                     break;
            case 4:  leds[i] = YELLOW;
                     break;
            case 5:  leds[i] = MAGENTA;
                     break;
            case 6:  leds[i] = CYAN;
                     break;
            default: break;
          }
        }
        count++;
        count %= 7;
      }

      delayCounter++;
      delayCounter %= 20; //Cycle once per second
    }
    void reset(){
      count = 0;
      delayCounter = 0;
    }
};

class Pew : public LEDMode {
  int count = 0;
  int colorCounter = 0;

  public:
    void doLoop() {

      long currentColor;

      switch (colorCounter){
        case 0:  currentColor = WHITE;
                 break;
        case 1:  currentColor = RED;
                 break;
        case 2:  currentColor = GREEN;
                 break;
        case 3:  currentColor = BLUE;
                 break;
        default: break;
      }

      for (int i = 0; i < NUM_LEDS; i++){
        if (i == (NUM_LEDS / 2 - 1) - count || i == (NUM_LEDS / 2) + count) leds[i] = currentColor;
        else                                    leds[i] = BLACK;
      }

      count++;
      count %= (NUM_LEDS / 2 - 1);

      if (count == 0) {
        colorCounter++;
        colorCounter %= 4;
      }
    }
    void reset(){
      count = 0;
      colorCounter = 0;
    }

};

SolidWhite* solidWhiteInst = new SolidWhite();
ColorCycle* colorCycleInst = new ColorCycle();
Marquee*    marqueeInst    = new Marquee();
Pew*        pewInst        = new Pew();


LEDMode* currentMode = solidWhiteInst;

void setup(){

  FastLED.addLeds<WS2801, DATA_PIN, CLOCK_PIN, RGB>(leds, NUM_LEDS);
  FastLED.clear();
  FastLED.show();

  Serial.begin(9600);
  Ethernet.begin(mac, ip);
  Serial.println("Starting to look");

  bool connection = false;
  while (!connection){
    if (client.connect(robotIP, port)){
      connection = true;
      Serial.println("Connected!");
    }
    else{
      Serial.println("waiting...");
      delay(1000);
    }
  }
}

void loop(){
  currentMode->doLoop();
  FastLED.show();
  delay(25);
  changeMode();
}

void changeMode(){
  if (client.available()){
    Serial.println("Got a stuff");
    byte c = client.read();
    switch (c){
      case SOLIDWHITE:  currentMode = solidWhiteInst;
                              break;
      case COLORCYCLE:  currentMode = colorCycleInst;
                              break;
      case MARQUEE:     currentMode = marqueeInst;
                              break;
      case PEW:         currentMode = pewInst;
                              break;
      default:                break;
    }
    currentMode->reset();
  }

  //If we lost connection, wait until reconnected
  if (!client.connected()){
    FastLED.clear();
    FastLED.show();
    Serial.println("Lost connection. Dammit.");
    bool connection = false;
    while (!connection){
      if (client.connect(robotIP, port)){
        connection = true;
        Serial.println("Connected!");
      }
      else{
        delay(1000);
        Serial.println("waiting...");
      }
    }
  }
}

void post_frame() {
  Serial.println("start post");
  FastLED.show();
  Serial.println("done show");
  delay(1000);
  Serial.println("done delay");
  
}
